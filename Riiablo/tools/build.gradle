project.ext.visuiVersion = '1.4.2'

subprojects {
  apply plugin: 'application'

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

  java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  run {
    ignoreExitValue true

    // Required to run LWJGL3 java apps on MacOS
    if (org.gradle.internal.os.OperatingSystem.current() == org.gradle.internal.os.OperatingSystem.MAC_OS) {
      jvmArgs += "-XstartOnFirstThread"
    }
  }

  jar {
    archiveBaseName = project.name
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    dependsOn configurations.runtimeClasspath
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
      attributes 'Main-Class': application.mainClass
    }

    doLast {
      file(archiveFile).setExecutable(true, false)
    }
  }
}
